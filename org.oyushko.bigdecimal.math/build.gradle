plugins {
    id 'org.sonarqube' version '2.7'
    id 'java'
    id 'eclipse'
    id 'maven'
    id 'signing'
    id 'jacoco'
}

group = 'org.oyushko'
version = '0.1.0'
archivesBaseName = 'bigdecimal-math'

ext {
    moduleName = "org.oyushko.bigdecimal.math"
    if (!project.hasProperty("ossrhUsername")) {
        ossrhUsername = "undefined"
    }
    if (!project.hasProperty("ossrhPassword")) {
        ossrhPassword="undefined"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

compileJava   {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

dependencies {
    testCompile 'junit:junit:4.13.1'
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "bigdecimal-math"
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

jar {
    manifest {
        attributes(
                "Automatic-Module-Name": moduleName,
                "Bundle-ManifestVersion": 2,
                "Bundle-Name": archivesBaseName,
                "Bundle-SymbolicName": moduleName,
                "Bundle-Version": version,
                "Export-Package": "org.oyushko.bigdecimal.math, org.oyushko.bigdecimal.math.stream")
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    //archives jar // no longer needed tp add - will lead to duplicate
    archives sourcesJar
    archives javadocJar
}

signing {
    required {
        gradle.taskGraph.hasTask("uploadArchives")
    }

    sign configurations.archives
}

uploadArchives {
	repositories {
	    mavenDeployer {
	        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
	
	        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
	            authentication(userName: ossrhUsername, password: ossrhPassword)
	        }
	
	        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
	            authentication(userName: ossrhUsername, password: ossrhPassword)
	        }
	
	        pom.project {
	            name 'BigDecimal Math'
	            packaging 'jar'
	            // optionally artifactId can be defined here 
	            description 'Math functions for BigDecimal.'
	            url 'https://github.com/runforlife/bigdecimal-math'
	
	            scm {
	                connection 'scm:git:git://github.com/bigdecimal-math.git'
	                developerConnection 'scm:ssh://github.com:runforlife/bigdecimal-math.git'
	                url 'https://github.com/runforlife/bigdecimal-math/'
	            }
	
	            licenses {
	                license {
	                    name 'MIT License'
	                    url 'https://raw.githubusercontent.com/runforlife/bigdecimal-math/master/LICENSE.txt'
	                }
	            }
	
	            developers {
	                developer {
	                    id 'runforlife'
	                    name 'Oleg Yushko'
	                }
	            }
	        }
	    }
	}
}